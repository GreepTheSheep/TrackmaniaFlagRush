#Include	"Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_ColorPalette.Script.txt" as Colors

#Const C_FlagMarkerHeightIndicatorThreshold	7.

/**
 *	Common Functions to embed in multiple manialink
 */
 Text GetTeamColorNetreadFunctions() {
	return """
			Vec3 GetTeamPrimaryColor(CTeam Team) {
				if (Team == Null) return {{{ Colors::C_NeutralLight }}};
				declare netread Vec3 Net_FlagRush_PrimaryColor for Team;
				return Net_FlagRush_PrimaryColor;
			}

			Vec3 GetTeamPrimaryColor(Integer Clan) {
				if (Clan <= 0 || Clan > Teams.count) return {{{ Colors::C_NeutralLight }}};
				return GetTeamPrimaryColor(Teams[Clan-1]);
			}
""";
}

Text GetUtilFunctions() {
	return """
		CSmPlayer GetPlayer(Text Login) {
			foreach (Player in Players) if (Player.User.Login == Login) return Player;
			return Null;
		}

		Vec3 GetPlayerPosition(CSmPlayer Player) {
			if(Player == InputPlayer || !Player.UseCrudeExtrapolation) return Player.Position;
			declare netread Vec3 Net_Position for Player;
			return Net_Position;
		}

		Real GetPlayerYaw(CSmPlayer Player) {
			if (Player == InputPlayer || !Player.UseCrudeExtrapolation) return Player.AimYaw;
			declare netread Real Net_Yaw for Player;
			return Net_Yaw;
		}
	""";
}