#Include "Libs/Zrx/CylinderHitbox.Script.txt" as Hitbox

/////////////
// Structs //
/////////////

#Struct Struct_CollisionEvent {
	CSmPlayer	Player1;
	CSmPlayer	Player2;
}

/////////////
// Globals //
/////////////

declare Struct_CollisionEvent[] CollisionPendingEvents;

///////////////
// Functions //
///////////////

/**
 *	Get the collision events from the last frame
 */
Struct_CollisionEvent[] GetPendingEvents() {
	return CollisionPendingEvents;
}

/**
 *	Call this in MB_Private_Yield of the mode to update the collision events
 */
Void Yield() {
	CollisionPendingEvents.clear();

	// Collision detection using CylinderHitbox (based on Dommy's Lib)
	for(IndexPlayer1, 0, Players.count - 2) {
		for(IndexPlayer2, IndexPlayer1 + 1, Players.count - 1) {	// No need to check with previous players		
			declare Player1 = Players[IndexPlayer1];
			declare Player2 = Players[IndexPlayer2];
			if(Hitbox::AreTouching(Player1, Player2)) {
				declare Struct_CollisionEvent CollisionEvent = Struct_CollisionEvent{Player1 = Player1, Player2 = Player2};
				CollisionPendingEvents.add(CollisionEvent);
			}
		}
	}
}